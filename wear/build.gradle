import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-platform-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion sdk_target
    defaultConfig {
        applicationId "com.artemchep.horlogo"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode version_code
        versionName version_name
    }

    signingConfigs {
        release {
            def releaseProps = new Properties()
            if (file("horlogo-release.properties").exists()) {
                file("horlogo-release.properties").withInputStream {
                    releaseProps.load(it)
                }
            }

            storeFile = file("horlogo-release.keystore")
            keyAlias releaseProps['key_alias']
            keyPassword releaseProps['password_key']
            storePassword releaseProps['password_store']
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        signingConfigs.release.storePassword = System.getenv("password_store")
        signingConfigs.release.keyPassword = System.getenv("password_key")
        signingConfigs.release.keyAlias = System.getenv("key_alias")
    }

    Date date = Calendar.getInstance().getTime()
    String time = new SimpleDateFormat("EEE MMMM dd HH:mm:ss zzz yyyy", Locale.ENGLISH).format(date)
    String timeYear = new SimpleDateFormat("yyyy", Locale.ENGLISH).format(date)

    buildTypes {
        release {
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.release
        }

        // Add config fields to each of build types.
        buildTypes.each {
            it.buildConfigField "String", "MY_TIME", "\"" + time + "\""
            it.buildConfigField "String", "MY_TIME_YEAR", "\"" + timeYear + "\""
        }
    }
}

dependencies {
    implementation project(":common")
    implementation "com.artemchep.config:config:2.0.2"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    implementation "androidx.core:core-ktx:1.0.1"
    implementation "androidx.recyclerview:recyclerview:$androidx_lib_version"
    implementation "androidx.percentlayout:percentlayout:$androidx_lib_version"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.wear:wear:$androidx_lib_version"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    implementation "com.google.android.material:material:1.0.0"
    implementation "com.google.android.support:wearable:2.4.0"
    implementation "com.google.android.gms:play-services-wearable:16.0.1"

    compileOnly 'com.google.android.wearable:wearable:2.4.0'
}
